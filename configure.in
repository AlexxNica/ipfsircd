#
# ngIRCd -- The Next Generation IRC Daemon
# Copyright (c)2001 by Alexander Barton (alex@barton.de)
#
# Dieses Programm ist freie Software. Sie koennen es unter den Bedingungen
# der GNU General Public License (GPL), wie von der Free Software Foundation
# herausgegeben, weitergeben und/oder modifizieren, entweder unter Version 2
# der Lizenz oder (wenn Sie es wuenschen) jeder spaeteren Version.
# Naehere Informationen entnehmen Sie bitter der Datei COPYING. Eine Liste
# der an comBase beteiligten Autoren finden Sie in der Datei AUTHORS.
#
# $Id: configure.in,v 1.8 2001/12/27 01:44:49 alex Exp $
#
# $Log: configure.in,v $
# Revision 1.8  2001/12/27 01:44:49  alex
# - die Verwendung von syslog kann nun abgeschaltet werden.
#
# Revision 1.7  2001/12/27 00:37:07  alex
# - Erkennung der "portab header" geaendert, CFLAGS werden nun anders gesetzt.
#
# Revision 1.6  2001/12/25 22:01:47  alex
# - neue configure-Option "--enable-sniffer".
#
# Revision 1.5  2001/12/21 23:54:26  alex
# - zusaetzliche Debug-Ausgaben koennen eingeschaltet werden.
#
# Revision 1.4  2001/12/12 17:21:58  alex
# - Projektdatei fuer den Mac OS X Project Builder erstellt.
#
# Revision 1.3  2001/12/12 01:58:53  alex
# - Test auf socklen_t verbessert.
#
# Revision 1.2  2001/12/11 22:04:21  alex
# - Test auf stdint.h (HAVE_STDINT_H) hinzugefuegt.
#
# Revision 1.1.1.1  2001/12/11 21:53:04  alex
# Imported sources to CVS.
#

# -- Initialisierung --

AC_INIT
AC_CONFIG_SRCDIR(src/ngircd/ngircd.c)
AM_INIT_AUTOMAKE(ngircd,0.0.1-pre)
AM_CONFIG_HEADER(src/config.h)

# -- Variablen --

CFLAGS="-Wall -g $CFLAGS"

# -- C Compiler --

AC_PROG_CC
AC_LANG_C

# -- Hilfsprogramme --

AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_RANLIB

# -- Header --

AC_HEADER_STDC

AC_HEADER_TIME

AC_CHECK_HEADER(portab.h,[
	AC_CHECK_HEADER(imp.h,,AC_MSG_ERROR([Alex \"portability headers\" (portab.h an friends) not found!]))
	AC_CHECK_HEADER(exp.h,,AC_MSG_ERROR([Alex \"portability headers\" (portab.h an friends) not found!]))
],[
	AC_CHECK_HEADER(/usr/local/include/portab.h,,AC_MSG_ERROR([Alex \"portability headers\" (portab.h an friends) not found!]))
	AC_CHECK_HEADER(/usr/local/include/imp.h,,AC_MSG_ERROR([Alex \"portability headers\" (portab.h an friends) not found!]))
	AC_CHECK_HEADER(/usr/local/include/exp.h,,AC_MSG_ERROR([Alex \"portability headers\" (portab.h an friends) not found!]))
	CFLAGS="$CFLAGS -I/usr/local/include"
])

AC_CHECK_HEADERS([ \
	arpa/inet.h errno.h fcntl.h netinet/in.h stdint.h string.h \
	sys/socket.h sys/time.h unistd.h \
	],,[required C header missing!])

# -- Datentypen --

AC_MSG_CHECKING(whether socklen_t exists)
AC_TRY_COMPILE([
	#include <sys/socket.h>
	#include <sys/types.h>
	],[
	socklen_t a, b;
	a = 2; b = 4; a += b;
	],[
	AC_DEFINE(HAVE_socklen_t) AC_MSG_RESULT(yes)
	],[
	AC_MSG_RESULT(no)
])

# -- Funktionen --

AC_FUNC_MALLOC

AC_CHECK_FUNCS([ \
	gethostname inet_ntoa memmove memset select \
	socket strcasecmp strchr strerror strstr \
	],,[required function missing!])

# -- Libraries --

# -- Konfigurationsoptionen --

AC_ARG_ENABLE(syslog,
	[  --disable-syslog        disable syslog (autodetected by default)],
	[	if test "$enableval" = "yes"; then
			AC_CHECK_HEADER(syslog.h,AC_DEFINE(USE_SYSLOG, 1),AC_MSG_ERROR([Can't enable syslog: syslog.h not found!]))
		else
			AC_MSG_RESULT([disabling syslog])
		fi
	],
	[	AC_CHECK_HEADER(syslog.h,AC_DEFINE(USE_SYSLOG, 1))
	]
)

AC_ARG_ENABLE(debug,
	[  --enable-debug          show additional debug output],
	if test "$enableval" = "yes"; then
		AC_DEFINE(DEBUG, 1)
		AC_MSG_RESULT([enabling additional debug output])
	fi
)

AC_ARG_ENABLE(sniffer,
	[  --enable-sniffer        enable network traffic monitor (enables debug mode!)],
	if test "$enableval" = "yes"; then
		AC_DEFINE(DEBUG, 1)
		AC_MSG_RESULT([enabling additional debug output])
		AC_DEFINE(SNIFFER, 1)
		AC_MSG_RESULT([enabling network traffic monitor])
	fi
)

# -- Ausgabe --

AC_OUTPUT([Makefile MacOSX/Makefile src/Makefile src/ngircd/Makefile])

# -eof-
